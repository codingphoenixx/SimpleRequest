plugins {
    id 'java'
    id 'maven-publish'

}

group = 'dev.coph'
version = '4.5.3'

repositories {
    mavenCentral()
    maven {
        url = uri("http://repo.coph.dev/releases")
        allowInsecureProtocol = true
    }
}

dependencies {
    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'

    implementation 'org.eclipse.jetty:jetty-server:12.1.3'
    implementation 'org.eclipse.jetty:jetty-security:12.1.3'
    implementation 'org.eclipse.jetty:jetty-util:12.1.3'
    implementation 'org.eclipse.jetty.ee10.websocket:jetty-ee10-websocket-jakarta-server:12.1.2'

    implementation 'org.json:json:20250517'
    implementation 'dev.coph:simplelogger:3.0.0'
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    repositories {
        maven {
            name = "cophrepository"
            url = "http://repo.coph.dev/releases"
            allowInsecureProtocol = true
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = "simplerequest"
            version = this.version
            from components.java
        }
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

